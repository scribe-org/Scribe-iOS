# All Swedish (Q9027) nouns with their plural, gender and genitive forms.
# Enter this query at https://query.wikidata.org/.

# Note: does not include pronouns as the query wasn't running.

SELECT DISTINCT
  ?nominativeSingular ?nominativePlural
  ?genitiveSingular ?genitivePlural ?gender WHERE {

  # Nouns and pronouns.
  VALUES ?nounTypes { wd:Q1084 wd:Q147276 }
  ?lexeme a ontolex:LexicalEntry ;
    dct:language wd:Q9027 ;
    wikibase:lexicalCategory ?noun .
  FILTER(?noun = ?nounTypes)

  # Indefinite forms in both nominative and genitive.
  OPTIONAL {
    # Nominative Singular
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q110786 .

    # Nominative Plural
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 .

    # Genitive Singular
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q110786 .

    # Genitive Plural
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .

  # Definite forms in both nominative and genitive.
  OPTIONAL {
    # Nominative Singular
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?Q53997851 ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q110786 .

    # Nominative Plural
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 .

    # Genitive Singular
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q110786 .

    # Genitive Plural
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .

  # Optional selection of genders.
  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
    FILTER NOT EXISTS { ?lexeme wdt:P31 wd:Q202444 . }
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
