# All Swedish (Q9027) verbs and the currently implemented tenses for each.
# Enter this query at https://query.wikidata.org/.

SELECT
  ?activeInfinitive ?imperative ?activeSupine
  ?activePresent ?activePreterite
  ?passiveInfinitive ?passiveSupine
  ?passivePresent ?passivePreterite WHERE {

  ?lexeme a ontolex:LexicalEntry ;
    dct:language wd:Q9027 ;
    wikibase:lexicalCategory wd:Q24905 .

  # Active Voice

  # Infinitive (required)
  ?lexeme ontolex:lexicalForm ?activeInfinitiveForm .
  ?activeInfinitiveForm ontolex:representation ?activeInfinitive ;
    wikibase:grammaticalFeature wd:Q1317831 ;
    wikibase:grammaticalFeature wd:Q179230 ;

  # Imperative
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
      wikibase:grammaticalFeature wd:Q22716 ;
  } .

  # Supine
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeSupineForm .
    ?activeSupineForm ontolex:representation ?activeSupine ;
      wikibase:grammaticalFeature wd:Q1317831 ;
      wikibase:grammaticalFeature wd:Q548470 ;
  } .

  # Present
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePresentForm .
    ?activePresentForm ontolex:representation ?activePresent ;
      wikibase:grammaticalFeature wd:Q1317831 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .

  # Preterite
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePreteriteForm .
    ?activePreteriteForm ontolex:representation ?activePreterite ;
      wikibase:grammaticalFeature wd:Q1317831 ;
      wikibase:grammaticalFeature wd:Q442485 ;
  } .

  # Passive Voice

  # Infinitive
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveInfinitiveForm .
    ?passiveInfinitiveForm ontolex:representation ?passiveInfinitive ;
      wikibase:grammaticalFeature wd:Q1194697 ;
      wikibase:grammaticalFeature wd:Q179230 ;
  } .

  # Supine
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveSupineForm .
    ?passiveSupineForm ontolex:representation ?passiveSupine ;
      wikibase:grammaticalFeature wd:Q1194697 ;
      wikibase:grammaticalFeature wd:Q548470 ;
  } .

  # Present
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePresentForm .
    ?passivePresentForm ontolex:representation ?passivePresent ;
      wikibase:grammaticalFeature wd:Q1194697 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .

  # Preterite
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePreteriteForm .
    ?passivePreteriteForm ontolex:representation ?passivePreterite ;
      wikibase:grammaticalFeature wd:Q1194697 ;
      wikibase:grammaticalFeature wd:Q442485 ;
  } .
}
