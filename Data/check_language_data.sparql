# This query is to check the totals of a given language on Wikidata.
# Enter this query at https://query.wikidata.org/ and change the language where directed.

SELECT ?wordCategory (COUNT(?wordCategory) as ?wordCategoryCounts) WHERE {
  # Enter language QID here.
  # You can find the QID of the language you want to check by searching for it on https://www.wikidata.org/.
  FILTER( ?language = wd:Q1860 )

  # Checking nouns, proper nouns, pronouns, personal pronouns, verbs, adjectives, adverbs, prepositions, postpositions, conjunctions and articles.
  VALUES ?wordCategories { wd:Q1084 wd:Q147276 wd:Q36224 wd:Q468801 wd:Q24905 wd:Q34698 wd:Q380057 wd:Q4833830 wd:Q161873 wd:Q191536 wd:Q103184 }

  # Query the lexeme if it's in the above categories of words.
  ?lexeme a ontolex:LexicalEntry ;
    dct:language ?language ;
    wikibase:lexicalCategory ?category ;
  FILTER(?category = ?wordCategories )

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]" .
    ?category rdfs:label ?wordCategory .
  }
}
GROUP BY ?wordCategory
ORDER BY ?wordCategory
