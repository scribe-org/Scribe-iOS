# All Portuguese (Q5146) verbs and the currently implemented tenses for each.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  ?infinitive
  ?presFPS ?presSPS ?presTPS
  ?presFPP ?presSPP ?presTPP
  ?perfFPS ?perfSPS ?perfTPS
  ?perfFPP ?perfSPP ?perfTPP
  ?impFPS ?impSPS ?impTPS
  ?impFPP ?impSPP ?impTPP
  ?fSimpFPS ?fSimpSPS ?fSimpTPS
  ?fSimpFPP ?fSimpSPP ?fSimpTPP WHERE {

  ?lexeme a ontolex:LexicalEntry ;
    dct:language wd:Q5146 ;
    wikibase:lexicalCategory wd:Q24905 .

  # Infinitive (required)
  VALUES ?infTypes { wd:Q179230 wd:Q64003131 }
  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature ?inf ;
  FILTER(?inf = ?infTypes)

  VALUES ?perfectTypes { wd:Q23663136 wd:Q64005357 }

  # Present
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presFPSForm .
    ?presFPSForm ontolex:representation ?presFPS ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSPSForm .
    ?presSPSForm ontolex:representation ?presSPS ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPSForm .
    ?presTPSForm ontolex:representation ?presTPS ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presFPPForm .
    ?presFPPForm ontolex:representation ?presFPP ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSPPForm .
    ?presSPPForm ontolex:representation ?presSPP ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPPForm .
    ?presTPPForm ontolex:representation ?presTPP ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .

  # Past Perfect
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?perfFPSForm .
    ?perfFPSForm ontolex:representation ?perfFPS ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q23663136 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?perfSPSForm .
    ?perfSPSForm ontolex:representation ?perfSPS ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q23663136 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?perfTPSForm .
    ?perfTPSForm ontolex:representation ?perfTPS ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q23663136 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?perfFPPForm .
    ?perfFPPForm ontolex:representation ?perfFPP ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q23663136 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?perfSPPForm .
    ?perfSPPForm ontolex:representation ?perfSPP ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q23663136 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?perfTPPForm .
    ?perfTPPForm ontolex:representation ?perfTPP ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q23663136 ;
  } .

  # Past Imperfect
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impFPSForm .
    ?impFPSForm ontolex:representation ?impFPS ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q12547192 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impSPSForm .
    ?impSPSForm ontolex:representation ?impSPS ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q12547192 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impTPSForm .
    ?impTPSForm ontolex:representation ?impTPS ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q12547192 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impFPPForm .
    ?impFPPForm ontolex:representation ?impFPP ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q12547192 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impSPPForm .
    ?impSPPForm ontolex:representation ?impSPP ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q12547192 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impTPPForm .
    ?impTPPForm ontolex:representation ?impTPP ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q12547192 ;
  } .

  # Future Simple
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?fSimpFPSForm .
    ?fSimpFPSForm ontolex:representation ?fSimpFPS ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q623742 ;
      wikibase:grammaticalFeature wd:Q682111 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?fSimpSPSForm .
    ?fSimpSPSForm ontolex:representation ?fSimpSPS ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q623742 ;
      wikibase:grammaticalFeature wd:Q682111 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?fSimpTPSForm .
    ?fSimpTPSForm ontolex:representation ?fSimpTPS ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q623742 ;
      wikibase:grammaticalFeature wd:Q682111 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?fSimpFPPForm .
    ?fSimpFPPForm ontolex:representation ?fSimpFPP ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q623742 ;
      wikibase:grammaticalFeature wd:Q682111 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?fSimpSPPForm .
    ?fSimpSPPForm ontolex:representation ?fSimpSPP ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q623742 ;
      wikibase:grammaticalFeature wd:Q682111 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?fSimpTPPForm .
    ?fSimpTPPForm ontolex:representation ?fSimpTPP ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q623742 ;
      wikibase:grammaticalFeature wd:Q682111 ;
  } .
}
